module org/sugarj/transformations/LayoutSugar

imports org/sugarj/languages/Sdf2


signature
  constructors
    LayoutConstraint : LayoutConstraint() -> Sdf2ATermAttribute()
    LayoutInt        : Sdf2IntCon() -> LayoutConstraint()


signature
  constructors
    LayoutSelect : LayoutConstraint() * Sdf2AFun() -> LayoutConstraint()
    LayoutGT     : LayoutConstraint() * LayoutConstraint() -> LayoutConstraint()
    LayoutGE     : LayoutConstraint() * LayoutConstraint() -> LayoutConstraint()
    LayoutEQ     : LayoutConstraint() * LayoutConstraint() -> LayoutConstraint()
    LayoutLE     : LayoutConstraint() * LayoutConstraint() -> LayoutConstraint()
    LayoutLT     : LayoutConstraint() * LayoutConstraint() -> LayoutConstraint()
    LayoutAnd    : LayoutConstraint() * LayoutConstraint() -> LayoutConstraint()
    LayoutOr     : LayoutConstraint() * LayoutConstraint() -> LayoutConstraint()
    LayoutNot    : LayoutConstraint() -> LayoutConstraint()


strategies
  desugar =
    changed-tree(desugar-layout)


rules

  desugar-layout :
    LayoutConstraint(c) -> default(
                             appl(
                               unquoted("layout")
                             , [<layout-to-aterm> c]
                             )
                           )

  layout-to-aterm :
    LayoutInt(i) -> int(i)

  layout-to-aterm :
    LayoutSelect(c, f) -> appl(f, [<layout-to-aterm> c])

  layout-to-aterm :
    LayoutNot(c) -> appl(
                      unquoted("not")
                    , [<layout-to-aterm> c]
                    )

  layout-to-aterm :
    op#([l, r]) -> appl(
                     unquoted(fun)
                   , [<layout-to-aterm> l, <layout-to-aterm> r]
                   )
    where <layout-binop-to-funname> op => fun

  layout-binop-to-funname :
    "LayoutGT" -> "gt"

  layout-binop-to-funname :
    "LayoutGE" -> "ge"

  layout-binop-to-funname :
    "LayoutEQ" -> "eq"

  layout-binop-to-funname :
    "LayoutLE" -> "le"

  layout-binop-to-funname :
    "LayoutLT" -> "lt"

  layout-binop-to-funname :
    "LayoutAnd" -> "and"

  layout-binop-to-funname :
    "LayoutOr" -> "or"
