module org/sugarj/transformations/EditorServiceExtension

imports
  org/sugarj/languages/Stratego 
  org/sugarj/languages/SugarJ 

signature
  constructors
    Checking : lexEditorServiceSectionName() * cfiter-star() -> cfEditorServiceSection()
    CheckingRuleErrors   : CheckingRuleBody() -> CheckingRule()
    CheckingRuleWarnings : CheckingRuleBody() -> CheckingRule()
    CheckingRuleNotes    : CheckingRuleBody() -> CheckingRule()
    RuleNoCond           : StrategoTerm() * StrategoTerm() -> CheckingRuleBody()
    Rule                 : StrategoTerm() * StrategoTerm() * StrategoStrategy() -> CheckingRuleBody()
    Rule                 : StrategoTerm() * StrategoTerm() * iter() -> CheckingRuleBody()

rules
  
  desugar = changed-tree(checks-to-sugar)

  checks-to-sugar : 
    ChecksDec(ChecksDecHead(mods, name), ChecksBody(rs)) -> ExtensionDec(ExtensionDecHead(mods, name), ExtensionBody(<map(checkingrule-to-sugar)> rs))

  checkingrule-to-sugar :
    Rules(rs) -> transformation-elem(Rules(rs))
  
  checkingrule-to-sugar :
    CheckingRuleErrors(rs) -> transformation-elem(
                                   Rules(<map(!RDefNoArgs("constraint-error", <id>))> rs)
                                 )

  checkingrule-to-sugar :
    CheckingRuleWarnings(rs) -> transformation-elem(
                                   Rules(<map(!RDefNoArgs("constraint-warning", <id>))> rs)
                                   )

  checkingrule-to-sugar :
    CheckingRuleNotes(rs) -> transformation-elem(
                                   Rules(<map(!RDefNoArgs("constraint-note", <id>))> rs)
                                )
