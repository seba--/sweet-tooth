module org/sugarj/transformations/Renaming
imports
  libstratego-lib
  org/sugarj/languages/Stratego

rules
  apply-renamings = where(prim("SUGARJ_current_renamings") => renamings); try(rename-decl(|renamings))
  apply-renamings(|renamings) = rename-decl(|renamings)
  rename-decl(|renamings) = fail 

rules
  rename-rules(|oname, nname) = 
    topdown(strip-annos);
    topdown(try(rename-rule(|oname, nname)));
    topdown(try(SVar(?oname;!nname)))
  
  rename-rule(|oname, nname) :
    RDefNoArgs(oname, rule) -> RDefNoArgs(nname, rule)
    
  rename-rule(|oname, nname) :
    RDef(oname, typeids, rule) -> RDef(nname, typeids, rule)
    
  rename-rule(|oname, nname) :
    RDefT(oname, typeids1,typeids2, rule ) -> RDefT(nname, typeids1, typeids2, rule)
    
  rename-rule(|oname, nname) :
    SDefNoArgs(oname, strategy) -> SDefNoArgs(nname, strategy)
    
  rename-rule(|oname, nname) :
    SDef(oname, typeids, strategy) -> SDef(nname, typeids, strategy)
